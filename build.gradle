apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
	mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "net.davidtanzer"
archivesBaseName = "jobjectformatter"
version = '0.2.0'

dependencies {
	testCompile 'org.hamcrest:hamcrest-all:1.3'
	testCompile 'junit:junit:4.11'
	testCompile 'org.mockito:mockito-core:1.9.5'
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

signing {
	sign configurations.archives
}

def ossrhUsername = hasProperty('ossrhUsername') ? ossrhUsername : ''
def ossrhPassword = hasProperty('ossrhPassword') ? ossrhPassword : ''

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			pom.project {
				name 'jObjectFormatter'
				packaging 'jar'
				description 'jObjectFormatter - A toString Builder for Java'
				url 'https://github.com/dtanzer/jobjectformatter'

				scm {
					connection 'scm:git:https://github.com/dtanzer/jobjectformatter.git'
					developerConnection 'scm:git:https://github.com/dtanzer/jobjectformatter.git'
					url 'https://github.com/dtanzer/jobjectformatter'
				}

				licenses {
					license {
						name 'The Apache License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}

				developers {
					developer {
						id 'dtanzer'
						name 'David Tanzer'
						email 'business@davidtanzer.net'
					}
				}
			}
		}
	}
}
